
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.отусБалансВиртуальныхКарт.БлокироватьДляИзменения = Истина;
	
	// регистр отусБалансВиртуальныхКарт Расход
	Движения.отусБалансВиртуальныхКарт.Записывать = Истина; 
	
	Для Каждого ТекСтрокаКарты Из Карты Цикл
		Движение = Движения.отусБалансВиртуальныхКарт.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Карта = ТекСтрокаКарты.ВиртуальнаяКарта;
		Движение.Сумма = ТекСтрокаКарты.Сумма;
	КонецЦикла;
	
	//Запишем движения
	Движения.Записать();  
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтусСписаниеБонусовКарты.ВиртуальнаяКарта КАК ВиртуальнаяКарта,
		|	СУММА(ОтусСписаниеБонусовКарты.Сумма) КАК Сумма
		|ПОМЕСТИТЬ Вт_ТЧ
		|ИЗ
		|	Документ.ОтусСписаниеБонусов.Карты КАК ОтусСписаниеБонусовКарты
		|ГДЕ
		|	ОтусСписаниеБонусовКарты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтусСписаниеБонусовКарты.ВиртуальнаяКарта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Вт_ТЧ.ВиртуальнаяКарта КАК ВиртуальнаяКарта,
		|	Вт_ТЧ.Сумма КАК Сумма,
		|	ЕСТЬNULL(отусБалансВиртуальныхКартОстатки.СуммаОстаток, 0)  КАК Нехватка
		|ИЗ
		|	Вт_ТЧ КАК Вт_ТЧ
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрНакопления.отусБалансВиртуальныхКарт.Остатки(
		|			,
		|			Карта В
		|				(ВЫБРАТЬ
		|					Вт_ТЧ.ВиртуальнаяКарта
		|				ИЗ
		|					Вт_ТЧ КАК Вт_ТЧ)) КАК отусБалансВиртуальныхКартОстатки
		|	ПО Вт_ТЧ.ВиртуальнаяКарта = отусБалансВиртуальныхКартОстатки.Карта
		|ГДЕ
		|	-ЕСТЬNULL(отусБалансВиртуальныхКартОстатки.СуммаОстаток, 0) > 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не хватает '", "ru")
		+ ВыборкаДетальныеЗаписи.Нехватка
		+ НСтр("ru = ' баллов для списания'", "ru"); 
		
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Отказ = Истина;
	
	КонецЦикла;
	

	
КонецПроцедуры
